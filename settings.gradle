pluginManagement {
    apply from: 'versions.gradle'


    repositories {
        // Dokka artifacts location
        exclusiveContent {
            forRepository {
                maven {
                    url = "https://software.r3.com/artifactory/conclave-maven"
                }
            }
            // We want to be sure only Dokka artifacts are retrieved from this location
            filter {
                includeModuleByRegex("com\\.r3\\.conclave\\.dokka", ".*")
            }
        }

        gradlePluginPortal()
    }


    resolutionStrategy {
        eachPlugin {
            if (requested.id.id == 'org.jetbrains.kotlin.jvm' || requested.id.id == 'org.jetbrains.kotlin.plugin.serialization') {
                useVersion kotlin_version
            }
        }
    }
}

rootProject.name = "conclave-core-sdk"

include 'cpp'
include 'conclave-mail'
include 'conclave-common'
include 'conclave-enclave'
include 'conclave-host'
include 'conclave-web-host'
include 'conclave-client'
include 'conclave-web-client'
include 'conclave-init'
include 'internal-testing'
include 'plugin-enclave-gradle'
