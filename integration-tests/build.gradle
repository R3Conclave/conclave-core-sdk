apply from: "${rootProject.projectDir}/../versions.gradle"
apply from: "${rootProject.projectDir}/../common.gradle"

subprojects {
    apply plugin: 'java'

    ext.enclaveMode = findProperty("enclaveMode")?.toString()?.toLowerCase() ?: "simulation"
    ext.runtimeType = findProperty("runtimeType")?.toString()?.toLowerCase() ?: "graalvm"

    repositories {
        maven {
            //  Only Conclave artifacts should be present here
            url = "${rootProject.projectDir}/../build/repo"
        }
        apply from: "${rootProject.projectDir}/repositories.gradle"
    }

    // This will also configure Kotlin: https://kotlinlang.org/docs/gradle.html#gradle-java-toolchains-support
    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }

    tasks.withType(Test) {
        useJUnitPlatform()
        systemProperty "enclaveMode", enclaveMode
        systemProperty "runtimeType", runtimeType
        // 10 mins should be plenty time to run a single integration test. If not re-write the test! We want timely
        // feedback if a test is hanging.
        systemProperty "junit.jupiter.execution.timeout.default", "10 m"
        // TODO: Remove this once gramine reaches feature parity
        if (runtimeType == "gramine") {
            failFast = false
        }
    }
}
